#!/bin/sh
#
# Use judge's input to produce filtered input or output
# or scoring instructions.
#
# File:		jfilter
# Author:	Bob Walton (walton@deas.harvard.edu)
# Date:		Sat Jan 24 05:43:55 EST 2015
#
# The authors have placed this program in the public
# domain; they make no warranty and accept no liability
# for this program.
#
# RCS Info (may not be true date or author):
#
#   $Author: walton $
#   $Date: 2014/12/24 13:02:02 $
#   $RCSfile: jfilter,v $
#   $Revision: 1.35 $
#
# The next lines start tclsh \
trap "echo jfilter: Terminated by Signal" \
    HUP INT QUIT; \
tclsh "$0" "$@"; exit $status

set document "
jfilter \[options\] judging_input_file
jfilter \[options\] judging_input_file \\
                  filtered_input_file
jfilter \[options\] judging_input_file \\
                  output_file filtered_output_file

    A judging_input_file (e.g., a .jin file) has scoring
    parameter definition lines and marker lines, and
    judge's input to test a submitted program.  The
    parameter definition and marker lines define both
    scoring instructions for scoring any output produced
    from the judge's input in the file, and a means of
    dividing both the judge's input and all output pro-
    duced therefrom into test cases and into test groups
    of test cases.

    The 1-file-name command reads the judging_input_file
    (e.g., a .jin file) and prints scoring instructions
    that can be passed to the autojudge program.

    The 2-file-name command reads the judging_input_file
    and writes filtered version of that to the filtered_
    input_file.  The filtered version version simply has
    judging parameter definition and marker lines
    removed.

    The 3-file-name command reads the judging_input_file
    and the output_file (e.g., a .out, .fout, .test, or
    .ftest file), and writes a filtered version of the
    output_file to the filtered_output_file.  The fil-
    tered_output_file is the same as the output_file
    except each line is prefaced with one of the follow-
    ing characters:

    	+ Begin a test group and a test case.
	| Begin a test group.
	- Begin a test case.
	. Do not begin a test case or test group.

    The -c and -g options below cause these prefix char-
    acters to be omitted from the filtered_output_file.

    Options modify the above commands as follows:

        -v For the 2- and 3-file-name commands, when a
	   new group starts, print the group number on
	   a line by itself, and when a new case starts,
	   print the group number, a space, and then the
	   case number on a line by itself.  If the
	   judging output and input files correspond,
	   the jfilter -v standard output for the 2- and
	   3-file-name commands should match exactly.

        -c <group-number>:<case-number>
        -c <case-number>

	    For the 2- and 3-file-name commands, these
	    cause the commands to output just the por-
	    tion of the judge's input or the portion of
	    the output that relates to testing the
	    designated test case.  The filtered file can
	    be used as feedback for a submitter whose
	    submission first failed on the given test
	    case.

	    If the *ONE-GROUP* parameter value is `no'
	    (see below for parameter definitions), then
	    for test cases outside any test group, the
	    portion output consists just of the lines in
	    the designated test case, from the test case
	    beginning line through the last line that is
	    just before the beginning of the next test
	    case or the end of file.  Thus if there are
	    no test groups, only the designated test
	    case is output.

	    For test cases in test groups, the portion
	    output consists of the lines in the test
	    group containing the test case, up to and
	    including the lines in the test case.  If
	    the *ONE-GROUP* parameter value is `yes',
	    then for test cases outside any group the
	    file is copied from its beginning through
	    the lines in the test case.

	    If the output is the filtered_output_file,
	    the output lines are exact copies of output_
	    file lines, without any prefix characters
	    indicating test group or test case begin-
	    nings.

	    The test case is designated by designating
	    the test group that contains the test case
	    and the test case within the group.  The
	    test group is designated by <group-number>,
	    with groups being numbered 1, 2, 3, ... .
	    Lines before the first group are considered
	    to be in group 0.  The test case is designa-
	    ted by <case-number>, with test cases being
	    numbered 1, 2, 3, ...  within their group.
	    Lines before the beginning of the first case
	    in a group are considered to be in test case
	    0 of that group.

	    If just the <case-number> is given, and the
	    `<group-number>:' is omitted, the <group-
	    number> is 0.  This is useful if the file
	    has no groups.

        -g <group-number>:<case-number>
	-g <case-number>

	    Same as -c, BUT if the test case is in a
	    test group (not in group 0), include all the
	    lines of this entire test group, instead of
	    stopping with the designated case.

	-comment <comment-regexp>
	-case-head <output-case-head-regexp>
	-group-head <output-group-head-regexp>
	-separator <output-separator-regexp>
	-terminator <output-terminator-regexp>
	-output-case-head <output-case-head-regexp>
	-output-group-head <output-group-head-regexp>
	-output-separator <output-separator-regexp>
	-output-terminator <output-terminator-regexp>
	-input-case-head <input-case-head-regexp>
	-input-group-head <input-group-head-regexp>
	-input-separator <input-separator-regexp>
	-input-terminator <input-terminator-regexp>
	-end-head <input-end-head-regexp>
	-input-end-head <input-end-head-regexp>
	-case-mark <input-case-mark-regexp>
	-group-mark <input-group-mark-regexp>
	-end-mark <input-end-mark-regexp>
	-input-case-mark <input-case-mark-regexp>
	-input-group-mark <input-group-mark-regexp>
	-input-end-mark <input-end-mark-regexp>
	-one-group <yes-or-no>
	-keep-head <yes-or-no>
	-count <yes-or-no>
	-scoring <scoring-instructions>

	    Set the default value of the designated
	    parameter.  These parameters can then be
	    reset by parameter definitions in the
	    judging_input_file.


    The judging_input_file begins zero or more parameter
    definitions, each having one of the following forms:

    *COMMENT* <comment-regexp>

    *CASE-HEAD* <output-case-head-regexp>
    *GROUP-HEAD* <output-group-head-regexp>
    *SEPARATOR* <output-separator-regexp>
    *TERMINATOR* <output-terminator-regexp>

    *OUTPUT-CASE-HEAD* <output-case-head-regexp>
    *OUTPUT-GROUP-HEAD* <output-group-head-regexp>
    *OUTPUT-SEPARATOR* <output-separator-regexp>
    *OUTPUT-TERMINATOR* <output-terminator-regexp>

    *INPUT-CASE-HEAD* <input-case-head-regexp>
    *INPUT-GROUP-HEAD* <input-group-head-regexp>
    *INPUT-SEPARATOR* <input-separator-regexp>
    *INPUT-TERMINATOR* <input-terminator-regexp>

    *END-HEAD* <input-end-head-regexp>
    *INPUT-END-HEAD* <input-end-head-regexp>

    *CASE-MARK* <input-case-mark-regexp>
    *GROUP-MARK* <input-group-mark-regexp>

    *INPUT-CASE-MARK* <input-case-mark-regexp>
    *INPUT-GROUP-MARK* <input-group-mark-regexp>

    *END-MARK* <input-end-mark-regexp>
    *INPUT-END-MARK* <input-end-mark-regexp>

    *ONE-GROUP* <yes-or-no>
    *KEEP-HEAD* <yes-or-no>
    *COUNT* <yes-or-no>

    *SCORING* <scoring-instruction> ...

    These consist of a parameter-designator, the first
    thing on the line (e.g., *CASE-HEAD*), followed by
    parameters.  In a parameter designator, `OUTPUT-'
    can be omitted: *CASE-HEAD* and *OUTPUT-CASE-HEAD*
    mean the same thing.  `INPUT-' can be omitted for
    the `MARK's: *CASE-MARK* and *INPUT-CASE-MARK* mean
    the same thing.  `INPUT-' can be omitted for the
    `END's: *INPUT-END-HEAD* and *END-HEAD* mean the
    same thing.  It is not that common to have
    *INPUT-...* parameters in a judging_input_file, as
    typically the file just uses the default *INPUT-...*
    parameter values (see below).

    Here `END' refers to the `end case', which is a
    special case that must always end the input, and
    which must be tacked on to the end of any other case
    or group that is extracted from the input.  INPUT-
    END is like INPUT-CASE but always includes its
    `end case' in any filtered_input_file.

    The regexp parameters are TCL regular expressions
    that are matched to lines of files.  Input regexps
    match lines in the judging_input_file, while output
    regexps match lines in the output_file.  A line
    matched by a `head' regexp is called a head line,
    and begins a test group, test case, or end case.  A
    line matched by a `separator' regexp is called a
    separator line, and a line matched by a `terminator'
    regexp is called a terminator line.  Both separator
    and terminator lines are the last line of a test
    case and are followed by the first line of another
    test case or by the end of file.  A line matched by
    a `mark' is like a head line but must be deleted
    from the filtered version of the file: marks are
    only used for the judging_input_file, to mark the
    beginning of test groups, test cases, and end cases.

    The only difference between a separator line and a
    terminator line is that if the output would other-
    wise end with a separator line, that line is sup-
    pressed and NOT included in the output, but if the
    last line of output would be a terminator line,
    that line IS included in the output.  One cannot use
    both separators and terminators in input, and simi-
    larly one cannot use both in output.

    If a line can be recognized as both a head and a
    mark, it is recognized as a mark.  If a line can
    is recognized as an end head or mark, that line and
    all subsequent lines will not be recognized as a
    case or group head or mark, or as a separator or
    terminator.

    A regexp parameter set to the empty string (all
    whitespace) is `missing', and deactivates use of
    the parameter in line matching.  It is an error if
    both the *INPUT-SEPARATOR* and *INPUT-TERMINATOR*
    are active, or if both the *OUTPUT-SEPARATOR* and
    *OUTPUT-TERMINATOR* are active.

    Any line matching the comment regexp is ignored.
    Comments are even recognized among parameters, but
    comment lines before any *COMMENT* parameter are
    recognized using program option or the default value
    of the comment regexp.

    If the *KEEP-HEAD* parameter is `no', the head
    (first) line of a test case is deleted in 2-file
    output.

    If the *COUNT* parameter is `yes', the number of
    test cases output is prefixed on a single line to
    the output.

    The *SCORING* parameters are just exactly what is
    printed on a single line by the 1-file-name command.

    Parameter definitions can be continued across lines
    by writing a sequence of lines with the same para-
    meter-designator.  Thus

    	*CASE-HEAD* xxx
    	*CASE-HEAD* yyy

    is the same as

    	*CASE-HEAD* xxxyyy

    Note that there is no space here between the xxx and
    yyy when they are concatenated.  This is because
    they are regexp parameters.  If they were non-regexp
    parameters, i.e. scoring parameters, a single space
    character would be inserted.  Therefore

    	*SCORING* xxx
    	*SCORING* yyy

    is the same as

    	*SCORING* xxx yyy

    Regexps should begin with `^' if they are to match
    just the beginning of a line.  Otherwise they will
    match any part of a line.

    Regexps are matched with a -nocase option, so letter
    case in the regexp makes no difference, unless the
    regexp begins with a `$', in which case the regexp
    without the beginning `$' is matched without the
    -nocase option (so the match is case sensitive).

    Whitespace is trimmed from the beginning and end of
    any regexp in a parameter definition.  This means
    that if the regexp is to end with a space character,
    it should be postpended by `()', which does nothing
    but mark the end of the regexp.  E.g.,

        *CASE-HEAD* ^foo ()


    The defaults for all parameters are

	*COMMENT* ^\[ \\t\]*\\*\\*\\*\\*

	*CASE-HEAD* ^
	*GROUP-HEAD*
	*SEPARATOR*
	*TERMINATOR*

	*INPUT-CASE-HEAD*
	*INPUT-GROUP-HEAD*
	*INPUT-SEPARATOR*
	*INPUT-TERMINATOR*

	*END-HEAD*

	*CASE-MARK* ^\\*CASE*\\$
	*GROUP-MARK* ^\\*GROUP\\*$

	*END-MARK* ^\\*END\\*$

	*ONE-GROUP* no
	*KEEP-NAME* yes
	*COUNT* no

	*SCORING* none

    These mean that by default input is separated into
    test groups, test cases, and end cases by `*GROUP*',
    `*CASE*', and `*END*' mark lines, every line of out-
    put is a separate test case, and there are no output
    test groups.  The default scoring instructions value
    of `none' specifies that the scoring_instructions
    global variable set in hpcm_judging.rc and any prob-
    lem .rc file should be used.  The default comment
    lines are any line whose first non-whitespace is
    `****'.

    The test group and test case judging input comes
    after any parameter definitions in the judging_
    input_file.  If there are no test groups, the input
    consists of just a sequence of test cases.  If there
    are test groups, test cases are grouped.  A single
    line may begin both a test case and a test group, in
    either input or output, or a test group may begin
    before the first test case in the group.

    Unlike most HPCM judging programs, this program does
    not read any hpcm_judging.rc file and errors made by
    this program are not written to log files.
"

# Execute regexp with -nocase if the regexp argument
# does not begin with `$', or without -nocase and with
# the beginning `$' removed from the regexp argument if
# the latter does begin with `$'.
#
proc jfregexp { regexp string } {
    if { [string index $regexp 0] == "$" } {
        return [regexp -- [string range $regexp 1 end] \
	               $string]
    } else {
        return [regexp -nocase -- $regexp $string]
    }
}

# Process options.
#
set comment "^\[ \\t\]*\\*\\*\\*\\*"
set output_case_head "^"
set output_group_head ""
set output_separator ""
set output_terminator ""
set input_case_head ""
set input_group_head ""
set input_separator ""
set input_terminator ""
set input_end_head ""
set input_case_mark "\\*CASE\\*"
set input_group_mark "\\*GROUP\\*"
set input_end_mark "\\*END\\*"
set one_group no
set keep_head yes
set count no
set scoring "none"
set c ""
set g ""
set v no
while { [regexp {^-} [lindex $argv 0]] } {
    set skip 2
    switch -- [lindex $argv 0] {
    -doc {
	    exec << $document less -F >&@ stdout
	    exit 0
	 }
    -comment		{ set comment [lindex $argv 1] }
    -case-head		-
    -output-case-head	{ set output_case_head \
    			      [lindex $argv 1] }
    -group-head		-
    -output-group-head	{ set output_group_head \
    			      [lindex $argv 1] }
    -separator		-
    -output-separator	{ set output_separator \
    			      [lindex $argv 1] }
    -terminator		-
    -output-terminator	{ set output_terminator \
    			      [lindex $argv 1] }
    -input-case-head	{ set input_case_head \
    			      [lindex $argv 1] }
    -input-group-head	{ set input_group_head \
    			      [lindex $argv 1] }
    -input-separator	{ set input_separator \
    			      [lindex $argv 1] }
    -input-terminator	{ set input_terminator \
    			      [lindex $argv 1] }
    -end-head		-
    -input-end-head	{ set input_end_head \
    			      [lindex $argv 1] }
    -case-mark		-
    -input-case-mark	{ set input_case_mark \
    			      [lindex $argv 1] }
    -group-mark		-
    -input-group-mark	{ set input_group_mark \
    			      [lindex $argv 1] }
    -end-mark		-
    -input-end-mark	{ set input_end_mark \
    			      [lindex $argv 1] }
    -scoring		{ set scoring \
    			      [lindex $argv 1] }
    -one-group		{ set one_group \
    			      [lindex $argv 1] }
    -keep-head		{ set keep_head \
    			      [lindex $argv 1] }
    -count		{ set count \
    			      [lindex $argv 1] }
    -c			{ set c \
    			      [lindex $argv 1] }
    -g			{ set g \
    			      [lindex $argv 1] }
    -v			{ set v yes
    			  set skip 1 }
    default {
    	    puts stderr "BAD option: [lindex $argv 0]"
	    exit 1
	    }
    }

    set argv [lrange $argv $skip end]
}

if { [llength $argv] < 1 || [llength $argv] > 3 } {
    puts stderr "ERROR: too few or too many arguments"
    exit 1
}

if { $c != "" && $g != "" } {
    puts stderr "ERROR: both -c and -g options given"
    exit 1
}

# Set up arguments.
#
set jin_file [lindex $argv 0]
set number_args [llength $argv]
switch $number_args {
    1 {}
    2 { set in_file [lindex $argv 1] }
    3 { set out_file [lindex $argv 1]
        set jout_file [lindex $argv 2] }
}

# Read and process parameter definitions.
#
if { [catch { set jin [open $jin_file r] }] } {
    puts stderr "ERROR: cannot open $jin_file"
    exit 1
}
set jin_line [gets $jin]

set last_designator ""
while { ! [eof $jin] } {

    if {    $comment != "" \
         && [jfregexp $comment $jin_line] } {
	set jin_line [gets $jin]
        continue
    }

    set designator ""
    set variable ""
    set separator ""
    regexp {^[\ \t]*([^\ \t]+)([\ \t].*|)$} $jin_line \
           forget designator rest
    switch -- $designator {
    *COMMENT*
        { set variable comment }
    *CASE-HEAD*	-
    *OUTPUT-CASE-HEAD*
        { set variable output_case_head }
    *GROUP-HEAD* -
    *OUTPUT-GROUP-HEAD*
        { set variable output_group_head }
    *SEPARATOR* -
    *OUTPUT-SEPARATOR*
        { set variable output_separator }
    *TERMINATOR* -
    *OUTPUT-TERMINATOR*
        { set variable output_terminator }
    *INPUT-CASE-HEAD*
        { set variable input_case_head }
    *INPUT-GROUP-HEAD*
        { set variable input_group_head }
    *INPUT-SEPARATOR*
        { set variable input_separator }
    *INPUT-TERMINATOR*
        { set variable input_terminator }
    *END-HEAD*	-
    *INPUT-END-HEAD*
        { set variable input_end_head }
    *CASE-MARK*	-
    *INPUT-CASE-MARK*
        { set variable input_case_mark }
    *GROUP-MARK* -
    *INPUT-GROUP-MARK*
        { set variable input_group_mark }
    *END-MARK*	-
    *INPUT-END-MARK*
        { set variable input_end_mark }
    *SCORING*
        { set variable scoring
	  set separator " " }
    *ONE-GROUP*
        { set variable one_group }
    *KEEP-HEAD*
        { set variable keep_head }
    *COUNT*
        { set variable count }
    default { break }
    }
    set rest [string trim $rest]
    if { $last_designator != $designator } {
        set $variable $rest
    } elseif { $rest != "" } {
        set $variable "[set $variable]$separator$rest"
    }
    set last_designator $designator
    set jin_line [gets $jin]
}

# Process 1 argument case.
#
if { $number_args == 1 } {
    close $jin
    puts $scoring
    exit 0
}

# Pre-process -c and -g options.
#
if { $c != "" && $g != "" } {
    puts stderr "ERROR: both -c and -g options given"
    exit 1
}
set cg "$c$g"
if { $cg != "" } {
    set copy 0
    set selective_copy 1
    set selected_group 0
    if {    ! [regexp {^([0-9]+):([0-9]+)$} $cg forget \
                      selected_group selected_case] \
         && ! [regexp {^([0-9]+)$} $cg forget \
	              selected_case] } {
	puts stderr "ERROR: bad -c or -g option: $cg"
	exit 1
    }
} else {
    set copy 1
    set selective_copy 0
}
if { $one_group != "yes" && $one_group != "no" } {
    puts stderr \
         "ERROR: bad *ONE-GROUP* value: $one_group"
    exit 1
}
if { $one_group } { set copy 1 }

# Pre-process separator and terminator options.
#
# A terminator is treated as a separator until just
# before the output file is closed.
#
if { $output_terminator != "" } {
    if { $output_separator != "" } {
	puts stderr "ERROR: both *OUTPUT-SEPARATOR* and\
		     *OUTPUT-TERMINATOR* parameters are\
		     active"
	exit 1
    }
    set output_separator $output_terminator
}
if { $input_terminator != "" } {
    if { $input_separator != "" } {
	puts stderr "ERROR: both *INPUT-SEPARATOR* and\
		     *INPUT-TERMINATOR* parameters are\
		     active"
	exit 1
    }
    set input_separator $input_terminator
}

# Process 2 argument case.
#
if { $number_args == 2 } {

    if { $count } {
        set i 0
	while { [file exists $in_file.$i] } {
	    incr i
	}
	set tmp_file $in_file.$i
	if { [catch { set in [open $tmp_file w] }] } {
	    puts stderr "ERROR: cannot open $tmp_file"
	    exit 1
	}
	if { [catch { set in_save [open $in_file w] }] \
	   } {
	    puts stderr "ERROR: cannot open $in_file"
	    exit 1
	}
    } else {
	if { [catch { set in [open $in_file w] }] } {
	    puts stderr "ERROR: cannot open $in_file"
	    exit 1
	}
    }

    # Copy is either always on if there is no selective
    # copying (i.e., no -c or -g option), or is turned
    # on and off appropriately to indicate whether a
    # non-mark line is to be copied.

    # State.
    #
    set group_count 0
    set case_count 0
    set copied_case_count 0
    set last_was_group_mark 0
    set last_was_case_mark \
        [expr { $input_separator != "" }]
    set last_was_end_mark 0
    set last_line ""
    set in_end 0
    set delayed_separator_line ""
    set delay 0

    while { ! [eof $jin] } {

	if {    $comment != "" \
	     && [jfregexp $comment $jin_line] } {
	    set jin_line [gets $jin]
	    continue
	}

	set begin_group $last_was_group_mark
	set begin_case $last_was_case_mark
	set begin_end $last_was_end_mark
	set is_mark 0
	set last_was_group_mark 0
	set last_was_case_mark 0
	set last_was_end_mark 0
	if { $in_end } {
	} elseif {    $input_group_mark != "" \
	           && [jfregexp $input_group_mark \
	                        $jin_line] } {
	    set last_was_group_mark 1
	    set is_mark 1
	} elseif {    $input_group_head != "" \
	           && [jfregexp $input_group_head \
		                $jin_line] } {
	    set begin_group 1
	}
	if { $in_end || $is_mark } {
	} elseif {    $input_end_mark != "" \
	           && [jfregexp $input_end_mark \
	                        $jin_line] } {
	    set last_was_end_mark 1
	    set is_mark 1
	    set in_end 1
	    set begin_group 0
	    set begin_case 0
	} elseif {    $input_end_head != "" \
	           && [jfregexp $input_end_head \
		                $jin_line] } {
	    set begin_end 1
	    set in_end 1
	    set begin_group 0
	    set begin_case 0
	} elseif {    $input_case_mark != "" \
	           && [jfregexp $input_case_mark \
	                        $jin_line] } {
	    set last_was_case_mark 1
	    set is_mark 1
        } elseif {    $input_case_head != "" \
	           && [jfregexp $input_case_head \
		                $jin_line] } {
	    set begin_case 1
	}

	if {    $input_separator != "" \
	     && [jfregexp $input_separator \
			  $jin_line] } {
	    set last_was_case_mark 1
	    set is_separator 1
	} else {
	    set is_separator 0
	}

        if { $begin_group } {
	    incr group_count 1
	    set case_count 0
	    if { $selective_copy } {
	        set copy [expr $group_count \
		               == $selected_group]
	    }
	    if { $v } {
	        puts $group_count
	    }
	}
	if { $begin_case } {
	    incr case_count
	    if {    $selective_copy \
		 && $selected_group == 0 \
		 && $group_count == 0 \
		 && $case_count == $selected_case } {
		    set copy 1
	    } elseif {    $c != "" \
	               &&   $case_count \
		          > $selected_case } {
	        set copy 0
	    }
	    if { $copy } {
	        incr copied_case_count
	    }
	    if { $v } {
	        puts "$group_count $case_count"
	    }
	}
	if { $begin_end } { set copy 1 }

	if { $delay } {
	    set delay 0
	    puts $in $delayed_separator_line
	}

	if { ! $is_mark && $copy } {
	    if { $is_separator } {
	        set delay 1
		set delayed_separator_line $jin_line
	    } elseif { $keep_head || ! $begin_case } {
		puts $in $jin_line
	    }
	}
	set last_line $jin_line

	set jin_line [gets $jin]
    }

    if { $delay && $input_terminator != "" } {
	puts $in $delayed_separator_line
    }
    close $jin
    close $in

    if { $count } {
        set in $in_save
	if { [catch { set tmp [open $tmp_file r] }] } {
	    puts stderr "ERROR: cannot open $tmp_file\
	                 for reading"
	    exit 1
	}
	puts $in $copied_case_count
	while { 1 } {
	    set line [gets $tmp]
	    if { [eof $tmp] } break
	    puts $in $line
	}
	close $tmp
	close $in
	file delete $tmp_file
    }
    exit 0
}

# Process 3 argument case.
#
if { $number_args == 3 } {

    close $jin
    if { [catch { set out [open $out_file r] }] } {
	puts stderr "ERROR: cannot open $out_file"
	exit 1
    }
    if { [catch { set jout [open $jout_file w] }] } {
	puts stderr "ERROR: cannot open $jout_file"
	exit 1
    }

    # State.
    #
    set group_count 0
    set case_count 0
    set last_line_was_separator \
        [expr { $output_separator != "" }]
    set delay 0
    set delayed_separator_line ""

    set out_line [gets $out]
    while { ! [eof $out] } {
        set group_head 0
	set case_head 0
	if {    $output_group_head != "" \
	     && [jfregexp $output_group_head \
		          $out_line] } {
	    incr group_count
	    set case_count 0
	    set group_head 1
	    if { $selective_copy } {
	        set copy [expr $group_count \
		               == $selected_group]
	    }
	    if { $v } {
	        puts $group_count
	    }
	}
        if {    (    $output_case_head != "" \
	          && [jfregexp $output_case_head \
		               $out_line] ) \
	     || $last_line_was_separator } {
	    incr case_count
	    set case_head 1
	    if {    $selective_copy \
		 && $selected_group == 0 \
		 && $group_count == 0 \
		 && $case_count == $selected_case } {
		    set copy 1
	    } elseif {    $c != "" \
	               &&   $case_count \
		          > $selected_case } {
	        set copy 0
	    }
	    if { $v } {
	        puts "$group_count $case_count"
	    }
	}

	set last_line_was_separator \
	    [expr {    $output_separator != "" \
	            && [jfregexp $output_separator \
		                 $out_line] }]
	
	if { $copy && ! $selective_copy } {
	    if { $group_head && $case_head } {
		set out_line "+$out_line"
	    } elseif { $group_head } {
		set out_line "|$out_line"
	    } elseif { $case_head } {
		set out_line "-$out_line"
	    } else {
		set out_line ".$out_line"
	    }
	}

	if { $delay } {
	    set delay 0
	    puts $jout $delayed_separator_line
	}
	if { $copy } {
	    if { $last_line_was_separator } {
	        set delay 1
		set delayed_separator_line $out_line
	    } else {
		puts $jout $out_line
	    }
	}

	set out_line [gets $out]
    }
    if { $delay && $output_terminator != "" } {
	puts $jout $delayed_separator_line
    }
    close $jout
    close $out

    exit 0
}

# Should not come here.
#
puts stderr "Programming error."
exit 1
