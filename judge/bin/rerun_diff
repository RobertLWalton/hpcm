#! /bin/sh
#
# Find differences in contest re-run from original
# contest.
#
# File:		diff_contest
# Author:	Bob Walton <walton@deas.harvard.edu>
# Date:		Thu Nov 16 14:16:08 EST 2006
#
# The authors have placed this program in the public
# domain; they make no warranty and accept no liability
# for this program.
#
# RCS Info (may not be true date or author):
#
#   $Author: walton $
#   $Date: 2006/11/16 19:57:15 $
#   $RCSfile: rerun_diff,v $
#   $Revision: 1.2 $
#
# The next line starts tcl \
exec tcl "$0" "$@"

# Include common code and parameters:
#
set lib_directory "[file dirname $argv0]/../lib"
source $lib_directory/judging_common.tcl
catch {

set document "
diff_contest \[original_dir\]

    Given directories named original_dir (default is
    \$judging_directory/ORIGINAL_MAIL') and \$judging_
    directory/mail', list all the submission directories
    in the original mail directory that have non-match-
    ing Auto_Score files in `mail'.  For each non-match-
    ing case output the differing Auto_Score files and
    the Subject line of the Received_Mail file.  Non-
    existing Auto_Score files are taken to have the
    value `does not exist' for the purposed of compari-
    son and output."

if {    [llength $argv] > 1 \
     || [regexp {^-doc} [lindex $argv 0]] } {
    puts $document
    exit 1
}

# Abbreviations:
#
set j_d [truename $judging_directory]

# Argument Processing.
#
if { [llength $argv] == 1 } {
    set o_dir [lindex $argv 0]
    set o_name [file tail $o_dir]
    if { $o_name == "mail" } {
        set o_name [file tail [file dirname $o_dir]]
    }
    set len [string length $o_name]
    if { $len < 4 } {
        set o_name [format {%4s} $o_name]
	set m_name mail
    } else {
        set m_name [format {%*s} $len mail]
    }
} else {
    set o_dir $j_d/ORIGINAL_MAIL
    set o_name "ORIGINAL_MAIL"
    set m_name "         mail"
}

puts "Judging directory is:"
puts "    $j_d"

if { ! [file isdirectory $o_dir] } {
    puts "ERROR: $o_dir does not exist"
    exit 1
}

set directories 0
set differences 0

set regexp {^.*<<.*>>-submission$}
foreach subdir \
        [lsort [glob -nocomplain $o_dir/*]] {
    set subdir [file tail $subdir]
    if { ! [regexp $regexp $subdir] } continue

    incr directories
    set source $o_dir/$subdir/Auto_Score
    set target $j_d/mail/$subdir/Auto_Score
    if { ! [file readable $source] } {
        set source "does not exist"
    } else {
        set source [read_entire_file $source]
    }
    if { ! [file readable $target] } {
        set target "does not exist"
    } else {
        set target [read_entire_file $target]
    }
    if { $source != $target } {
        set received $o_dir/$subdir/Received_Mail
	if { ! [file readable $received] } {
	    set subject "$received does not exist"
	} else {
	    set subject "No Subject field"
	    set in [open $received r]
	    while { 1 } {
	        set line [gets $in]
		if { [eof $in] } break
		if { [regexp -nocase \
		             {^Subject:} $line] } {
		    set subject $line
		    break
		}
	    }
	    close $in
	}
	puts $subdir
	puts "    $subject"
	puts "    $o_name Auto_Score: $source"
	puts "    $m_name Auto_Score: $target"
	incr differences
    }
}

puts "    $directories submission directories;\
          $differences have changed"

exit 0

# Include common error catching code:
#
} caught_output
caught_error
