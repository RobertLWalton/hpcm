Simple Vector Calculator
------ ------ ----------

You have been asked to implement a very simple vector
calculator.  The tokens of its input are

    <separator> ::= ( | ) | , | : | <end-of-line>
    <symbol> ::= <symbol-character> <symbol-character>*
    <symbol-character> ::=
        character other than a <separator>,
	single space, horizontal tab, and form feed

Given this the following are particular tokens:

    <variable> ::= <symbol> beginning with a letter
    <operator> ::= one of the following symbols:
    	+   -   *   /   ^   !   &&  ||  |
	==  !=  <   <=  >   >=

Whitespace is ignored except for its role in separating
tokens and for the <end-of-line> token.  Note that
operators must be delimited by whitespace or separators.

Blank lines are ignored.  Any line whose first non-
whitespace characters are `//' is a comment line and is
ignored.

Given these tokens, the syntax accepted by the
calculator is:

    <program> ::= <statement>*

    <statement> ::=
          <simple-statement>
        | if <boolean-atom>: <simple-statement>

    <simple-statement> ::=
          print <symbol>* <end-of-line>
        | println <symbol>* <end-of-line>
	| <variable> = <expression> <end-of-line>
	| clear <variable>* <end-of-line>

    <expression> ::= <boolean-expression>
                   | <scalar-expression>
		   | <vector-expression>

    <boolean-expression> ::=
          <boolean-atom>
	| ! <boolean-atom>
	| <boolean-atom> && <boolean-atom>
	| <boolean-atom> || <boolean-atom>
	| <scalar-atom> == <scalar-atom>
	| <scalar-atom> != <scalar-atom>
	| <scalar-atom> <  <scalar-atom>
	| <scalar-atom> <= <scalar-atom>
	| <scalar-atom> >  <scalar-atom>
	| <scalar-atom> >= <scalar-atom>

    <boolean-atom> ::=
          <variable> with boolean value
	| true
	| false

    <scalar-expression> ::=
          <scalar-atom>
	| - <scalar-atom>
	| | <scalar-atom> |
	| <scalar-atom> + <scalar-atom>
	| <scalar-atom> - <scalar-atom>
	| <scalar-atom> *  <scalar-atom>
	| <scalar-atom> / <scalar-atom>

    <scalar-atom> ::=
          <variable> with scalar value
	| <number>

    <vector-expression> ::=
          <vector-atom>
	| - <vector-atom>
	| || <vector-atom> ||
	| angle <vector-atom>
	| <vector-atom> + <vector-atom>
	| <vector-atom> - <vector-atom>
	| <scalar-atom> *  <vector-atom>
	| <vector-atom> ^ <scalar-atom>

    <vector-atom> ::=
          <variable> with vector value
        | ( <scalar-atom>, <scalar-atom> )

This syntax is very limited so it is easy to implement.
It has the following limits/peculiarities:

1. At most one operation such as + or - can appear in a
   statement.

2. Only very simple conditional statements are allowed,
   such as

       if x: y = 5 + z
       if m: y = true
       if p: print Y IS y

3. Operators such as +, *, |, and || must be delimited
   by whitespace, parentheses, commas, or colons.

The semantics are what you would expect given the
following:

1. Variables can have boolean, scalar, or vector values,
   or no values.  Variables with no values are only
   legal in print statements: see below.

2. The expression | s | denotes the absolute value of
   the scalar s.

3. The expression || v || denotes the length value of
   the vector.

4. Angles are measured in degrees.  Special care is
   taken with angles that are multiples of 90 degrees
   to be sure that rotations and the angle function
   give exact results.

5. The expression v ^ s denotes rotation of the vector
   v by the scalar angle s.
   
6. The print and println statements print symbols sepa-
   rated by single spaces.  But if a symbol is a vari-
   able name and the variable has a value, the value is
   printed instead of the symbol.  The `print' statement
   ends by printing just a single space so the line may
   be continued, while the `println' statement ends by
   printing a line end.

7. The `clear' statement clears the variables listed,
   meaning that these variables are given no value (and
   therefore print as their names).  If no variables are
   given, all variables are cleared.


Input
-----

Input is a program.  There is no limit on variable name
length or line length.  Input ends with an end of file.


Output
------

Output is the lines printed by print statements.


Sample Input
------ -----

// Sample vcalc Input
//
x = 5
y = -10
z = x + y
println x + y = z

x = true
y = false
v = ( 1, 0 )
w = v ^ 45
l = || w ||
a = angle w
if x: println v ^ 45 = w
if y: println this does not print
println || v ^ 45 || = l
println angle (  v ^ 45 ) = a

... see sample.in for rest of sample input ...


Sample Output
------ ------

5 + -10 = -5
(1, 0) ^ 45 = (0.707106781186548, 0.707106781186548)
|| (1, 0) ^ 45 || = 1
angle ( (1, 0) ^ 45 ) = 45

... see sample.test for rest of sample output ...


File:	   vcalc.txt
Author:	   Bob Walton <walton@seas.harvard.edu>
Date:	   Sat Jan 12 02:41:25 EST 2013

The authors have placed this file in the public domain;
they make no warranty and accept no liability for this
file.

RCS Info (may not be true date or author):

    $Author: walton $
    $Date: 2013/01/12 07:53:13 $
    $RCSfile: vcalc.txt,v $
    $Revision: 1.4 $
