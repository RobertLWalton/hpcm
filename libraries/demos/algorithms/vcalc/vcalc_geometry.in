// Example Geometry Algorithms Programmed in Vcalc
//
// File:	vcalc_geometry.in
// Author:	walton@seas.harvard.edu
// Date:	Mon Jan 14 08:28:12 EST 2013

// See Applications section of 2D Geometry help page.

println //
println // Distance Between Point and Infinite Line
p = (3,4)
p1 = (-5,-5)
p2 = (3, 3)
v = p2 - p1
length_v = || v ||
rv = v ^ 90
p'y = p * rv
p1'y = p1 * rv
p2'y = p2 * rv
not_equal = p1'y != p2'y
if not_equal println ERROR: p1'y != p2'y
dy = p'y - p1'y
abs_dy = | dy |
dist = abs_dy / length_v
println -- The distance from p
println ---- to the infinite line p1 -- p2
println ---- is dist

println //
print // Distance Between Point and Finite Line Segment
println
p = (3,4)
p1 = (-5,-5)
p2 = (3, 3)
v = p2 - p1
length_v = || v ||
rv = v ^ 90
p'x = p * v
p1'x = p1 * v
p2'x = p2 * v
p'y = p * rv
p1'y = p1 * rv
p2'y = p2 * rv
not_equal = p1'y != p2'y
if not_equal println ERROR: p1'y != p2'y
dy = p'y - p1'y
abs_dy = | dy |
dist_pp1p2 = abs_dy / length_v
p'x<p1'x = p'x < p1'x
pp1 = p - p1
dist_pp1 = || pp1 ||
p'x>p2'x = p'x > p2'x
pp2 = p - p2
dist_pp2 = || pp2 ||
either = p'x<p1'x || p'x>p2'x
neither = ! either
println -- The distance from p
println ---- to the finite line segment p1 -- p2
if p'x<p1'x: print ---- is distance from p to p1
if p'x<p1'x: println = dist_pp1
if p'x>p2'x: print ---- is distance from p to p2
if p'x>p2'x: println = dist_pp2
if neither: println ---- is the distance from p to the
if neither: print ---- infinite line p1 -- p2
if neither: println = dist_pp1p2

println //
println // Sides of an Infinite Line
p = (3,4)
p1 = (-5,-5)
p2 = (3, 3)
v = p2 - p1
length_v = || v ||
rv = v ^ 90
p'y = p * rv
p1'y = p1 * rv
p2'y = p2 * rv
not_equal = p1'y != p2'y
if not_equal println ERROR: p1'y != p2'y
RIGHT = p'y < p1'y
ON = p'y == p1'y
LEFT = p'y > p1'y
print -- The p is to the
if RIGHT: print right
if ON: print on
if LEFT: print left
println of the infinite line p1 -- p2
